extends "res://addons/gut/test.gd"

##### VARIABLES #####
#---- VARIABLES -----
var scene
var helper
var initial_preset_count

##### SETUP #####
func before_all():
	helper = load("res://test/integration/PlayerSelectionMenu/helper_player_selection_menu.gd").new()
	initial_preset_count = helper.count_saved_presets()
	helper.save_std_preset()

func before_each():
	scene = load("res://Scenes/UI/PlayerCustomizationMenu/player_selection_menu.tscn").instantiate()
	add_child_autofree(scene)
	await wait_for_signal(scene.tree_entered, 0.1)
	helper.set_selection_menu(scene)

##### TEARDOWN #####
func after_all():
	helper.remove_std_preset()
	await wait_seconds(0.1)
	helper.free()

##### TESTS #####
func test_add_remove_players():
	# given
	var items = helper.get_player_selection_items()
	# then 
	for item in items:
		assert_true(helper.is_add_player_visible(item))
	for item in items:
		# when
		helper.add_player_on_item(item)
		await wait_process_frames(3)
		# then
		assert_false(helper.is_add_player_visible(item))
		assert_true(helper.is_main_menu_visible(item))
	for item in items:
		# when
		helper.remove_player_on_item(item)
		await wait_process_frames(3)
		# then
		assert_true(helper.is_add_player_visible(item))
		assert_false(helper.is_main_menu_visible(item))

func test_main_menu() -> void:
	# given
	var items = helper.get_player_selection_items()
	var item = items[0]
	var config = load(StaticUtils.DEFAULT_CONFIG_PATH)
	helper.add_player_on_item(item)
	# then
	assert_true(helper.is_main_menu_visible(item))
	assert_true(helper.is_config_equals_display(config, item))
	# when
	config = load(helper.INTEGRATION_TEST_PRESET_PATH)
	helper.select_preset_config(config, item)
	# then
	assert_true(helper.is_config_equals_display(config, item))

func test_presets() -> void:
	# given
	var items = helper.get_player_selection_items()
	var item = items[0]
	# when
	helper.add_player_on_item(item)
	helper.select_presets_menu(item)
	var integration_test_config = helper.get_integration_test_config()
	var presets = helper.get_presets(item)
	var configs = helper.get_presets_configs(item)
	var total_preset_count = initial_preset_count + 1
	# then
	assert_false(helper.is_main_menu_visible(item))
	assert_true(helper.is_preset_menu_visible(item))
	assert_eq(presets.size(), total_preset_count)
	assert_true(helper.preset_buttons_contains_preset(presets, integration_test_config))
	# when
	helper.select_preset(presets[0])
	# then
	assert_false(helper.is_preset_menu_visible(item))
	assert_true(helper.is_main_menu_visible(item))
	assert_true(helper.is_config_equals_display(configs[0], item))
	
func test_primary_weapons() -> void:
	# given
	var items = helper.get_player_selection_items()
	var item = items[0]
	# when
	helper.add_player_on_item(item)
	helper.select_primary_weapon_menu(item)
	await wait_process_frames(3)
	# then
	assert_false(helper.is_main_menu_visible(item))
	assert_true(helper.is_primary_weapon_menu_visible(item))
	# when
	helper.select_primary_weapon(0, item)
	# then
	assert_true(helper.is_primary_weapon_selected(StaticPrimaryWeaponHandler.handlers.REVOLVER, item))
	assert_true(helper.is_main_menu_visible(item))
	assert_false(helper.is_primary_weapon_menu_visible(item))
	
func test_movement_bonus() -> void:
	# given
	var items = helper.get_player_selection_items()
	var item = items[0]
	# when
	helper.add_player_on_item(item)
	helper.select_movement_bonus_menu(item)
	await wait_process_frames(3)
	# then
	assert_false(helper.is_main_menu_visible(item))
	assert_true(helper.is_movement_bonus_menu_visible(item))
	# when
	helper.select_movement_bonus(0, item)
	# then
	assert_true(helper.is_movement_bonus_selected(StaticMovementBonusHandler.handlers.DASH, item))
	assert_true(helper.is_main_menu_visible(item))
	assert_false(helper.is_movement_bonus_menu_visible(item))

func test_powerup() -> void:
	# given
	var items = helper.get_player_selection_items()
	var item = items[0]
	# when
	helper.add_player_on_item(item)
	helper.select_powerup_menu(item)
	await wait_process_frames(3)
	# then
	assert_false(helper.is_main_menu_visible(item))
	assert_true(helper.is_powerup_menu_visible(item))
	# when
	helper.select_powerup(0, item)
	# then
	assert_true(helper.is_powerup_selected(StaticPowerupHandler.handlers.SPLITTER, item))
	assert_true(helper.is_main_menu_visible(item))
	assert_false(helper.is_powerup_menu_visible(item))